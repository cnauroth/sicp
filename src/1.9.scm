;
; Exercise 1.9
;
; Each of the following two procedures defines a method for adding two positive
; integers in terms of the procedures inc, which increments its argument by 1,
; and dec, which decrements its argument by 1.

(define (inc x)
  (+ x 1))

(define (dec x)
  (- x 1))

(define (adder1 a b)
  (if (= a 0)
      b
      (inc (adder1 (dec a) b))))

(define (adder2 a b)
  (if (= a 0)
      b
      (adder2 (dec a) (inc b))))

; Using the substitution model, illustrate the process generated by each
; procedure in evaluating (+ 4 5).  Are these processes iterative or recursive?

(display (adder1 4 5))
(newline)

; adder1 is recursive
; (adder1 4 5)
; (inc (adder1 3 5))
; (inc (inc (adder1 2 5)))
; (inc (inc (inc (adder1 1 5))))
; (inc (inc (inc (inc (adder1 0 5)))))
; (inc (inc (inc (inc 5))))
; (inc (inc (inc 6)))
; (inc (inc 7))
; (inc 8)
; 9

(display (adder2 4 5))
(newline)

; adder2 is iterative
; (adder2 4 5)
; (adder2 3 6)
; (adder2 2 7)
; (adder2 1 8)
; (adder2 0 9)
; 9
